apply plugin: 'com.android.application'

android {
    signingConfigs {
        config {
            keyAlias 'OpenWithElementum'
            keyPassword 'OpenWithElementum'
            storeFile file('../keys/OpenWithElementum.jks')
        }
    }
    compileSdkVersion 26

    def versionPropsFile = file('version.properties')
    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))

        def value = 0
        def runTasks = gradle.startParameter.getTaskNames()
        for (String item : runTasks) {
            if (item.contains('assembleRelease')) {
                value = 1;
                break;
            }
        }

        def versionMajor = 1
        def versionMinor = 3
        def versionPatch = versionProps['VERSION_PATCH'].toInteger() + value
        def versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1

        versionProps['VERSION_PATCH'] = versionPatch.toString()
        versionProps['VERSION_BUILD'] = versionBuild.toString()
        versionProps.store(versionPropsFile.newWriter(), null)

        def versionString = "${versionMajor}.${versionMinor}.${versionPatch}"
        if (!value) {
            versionString += " build ${versionBuild}"
        }

        defaultConfig {
            applicationId "su.css3.openwithelementum"
            minSdkVersion 19
            targetSdkVersion 26
            versionCode versionBuild
            versionName versionString
            setProperty("archivesBaseName", "OpenWithElementum-${versionMajor}.${versionMinor}.${versionPatch}")
        }
    } else {
        throw new GradleException("Could not read version.properties!")
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    dexOptions {
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'com.android.support:support-v4:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    api 'commons-codec:commons-codec:1.9'
}
